---
title: Terragrunt et dÃ©couverte de l'IAC en mode DRY
categories:
- iac
- terraform
date: "2025-03-27"
description: DÃ©couverte de Terragrunt et de l'IAC en mode DRY
summary: DÃ©couverte du wrapper Terragrunt, permettant de gÃ©rer des configurations Terraform en adoptant une approche DRY (Don't Repeat Yourself).
slug: terragrunt-terraform-dry
tags:
- cloud
- terraform
- iac
- terragrunt
showPagination:
  article: True

showTableOfContents:
  article: True

showComments: True

draft: False
---
{{< badge >}}
Nouvel article!
{{< /badge >}}

{{< lead >}}
Terragrunt est un wrapper (surcouche) pour Terraform, conÃ§ue pour simplifier et optimiser la gestion des configurations d'infrastructure en suivant le principe DRY (Don't Repeat Yourself). Dans cet article, je vais tenter d'expliquer le fonctionnement de Terragrunt Ã  travers un cas client fictif.
{{< /lead >}}

## Introduction

Reprenant les bonnes pratiques issues du monde du dÃ©veloppement, Terragrunt permet de rÃ©duire la duplication de code en factorisant les configurations Terraform. Cet article Ã  pour but d'expliquer la mise en place d'un projet Terraform avec Terragrunt, et surtout de comprendre lÃ  oÃ¹ Terragrunt apporte de la valeur par rapport Ã  Terraform seul. Attention, Terragrunt nÃ©cessite des bases **solides** en configurations Terraform, cet article se destine donc Ã  un public ayant dÃ©jÃ  pratiquÃ© Terraform sur des infrastructures moyennes Ã  grandes, et souhaitant en optimiser la gestion afin de passer Ã  l'Ã©chelle. Si ce n'est pas ton cas, je t'invite Ã  consulter [cette section](https://blog.stephane-robert.info/docs/infra-as-code/provisionnement/terraform/introduction/) du blog de StÃ©phane Robert pour te familiariser avec Terraform. Tout y est trÃ¨s bien expliquÃ©.

## Contexte

J'ai Ã©tÃ© amenÃ© Ã  travailler pour un client ayant une infrastructure cloud consÃ©quente, et logiquement beaucoup de ressources Ã  gÃ©rer. La principale problÃ©matique rencontrÃ©e dans la gestion d'une architecture de cette taille avec Terraform est pour moi l'organisation mÃªme du code, et la dette technique qui en dÃ©coule. En effet, Ã  force de multiplier les ressources, variables, environnements etc.. , on peut vite se retrouver avec du code difficile Ã  comprendre et maintenir, surtout lors de phases de build avec des deadlines serrÃ©es.

Terragrunt se veut Ãªtre une rÃ©ponse efficace Ã  ces problÃ©matiques en apportant une organisation **claire** et **modulaire** au code Terraform. En externalisant les paramÃ¨tres spÃ©cifiques Ã  chaque environnement et en automatisant la gestion des dÃ©pendances entre modules, Terragrunt offre une solution structurÃ©e pour minimiser la dette technique.

![plan-b-jeu](imgs/illustrations1-terraform.jpg "Illustration de circonstance : Plan B est un jeu proposant de ... Terraformer une planÃ¨te. Cool, non ?")

Cependant, son adoption n'est pas forcÃ©ment pertinente pour des projets de petite taille, et peut mÃªme Ãªtre contre-productive si mal utilisÃ©e *(ğŸ‘‹ [Kubernetes](https://www.appvia.io/blog/5-reasons-you-should-not-use-kubernetes))*. De plus, la courbe d'apprentissage peut Ãªtre assez raide, j'en ai fais les frais.

Comprendre les concepts de modularitÃ©, dâ€™hÃ©ritage de configurations ou encore de gestion des dÃ©pendances demande un investissement initial non nÃ©gligeable. Cela dit, une fois compris et bien utilisÃ©, Terragrunt peut vite se rÃ©vÃ©ler indispensable. Il permet de structurer efficacement des projets complexes, et de rÃ©duire la duplication de code (factorisation) de maniÃ¨re Ã©lÃ©gante.

Dans cet article, je vais donc tenter de t'expliquer comment dÃ©marrer un projet en me basant sur l'expÃ©rience que j'ai pu acquÃ©rir sur des infrastructures de grosses tailles.

Les exemples que je vais fournir se basent sur des configurations GCP, mais la logique est applicable Ã  n'importe quel cloud provider. Il ne s'agira pas ici d'expliquer comment bootstrap tel ou tel ressources, mais plutÃ´t de te montrer comment organiser ton code Terraform avec Terragrunt.

## C'est quoi concrÃ¨tement, Terragrunt

![schema-terragrunt](imgs/key-features-terraform-code-dry.png "SchÃ©ma reprÃ©sentatif d'une configuration Terragrunt, issu du site de Terragrunt")

Terragrunt est avant-tout un outil `CLI`, reprenant la syntaxe de Terraform dans son fonctionnement ad-hoc. On retrouvera donc les fameux `terragrunt plan`, `terragrunt apply`, etc.. LÃ  oÃ¹ Ã§a devient intÃ©ressant, c'est que Terragrunt propose en sus des fonctionnalitÃ©s comme la gÃ©nÃ©ration dynamiques de fichiers `backend.tf` (oui, il permet aussi de crÃ©er le `bucket` Ã  la volÃ©e si celui-ci n'existe pas), des [fonctions](https://terragrunt.gruntwork.io/docs/reference/built-in-functions/) permettant de manipuler des fichiers `HCL` (comme `read_terragrunt_config`), ou encore une gestion avancÃ©e des dÃ©pendances entre modules. Mais trÃªve de bavardages, la meilleure faÃ§on de comprendre Terragrunt est d'Ã©tudier un exemple concret.

## L'organisation du code

L'organisation du code, ainsi que l'architecture des dossiers sont **extrÃªmement** importante. Encore une fois, l'usage de Terragrunt permet de forcer des bonnes pratiques, mais un mauvais choix d'organisation peut vite se rÃ©vÃ©ler cauchemardesque, en particulier avec Terragrunt. Pour cet article, je vais donc choisir de baser mes explications sur le cas d'un client fictif, mais qui se rapproche de ce que j'ai pu voir dans le monde rÃ©el.

### Contexte client

Partons du principe suivant : je souhaite hÃ©berger pour le compte d'un client une application [3 tiers](https://fr.wikipedia.org/wiki/Architecture_trois_tiers) classique, afin d'hÃ©berger une marketplace. Niveau ressources cloud, on aura donc besoin de :

{{< alert "circle-info" >}}
Je travaille beaucoup sur GCP en ce moment, pour simplifier la rÃ©daction de mon article, je me base donc sur des assets GCP.
{{< /alert >}}

- Configurer un rÃ©seau VPC (subneting, firewall rules, etc..).
- Mettre en place une base de donnÃ©es
- DÃ©ployer une application web (front + back), dans mon example containÃ©risÃ©e
- Mettre en place un load balancer et un orchestrateur de conteneurs, par facilitÃ©e je vais ici utiliser CloudRun.

On parle d'IaC, il va donc falloir crÃ©er et organiser son code via des repos Git. Les dÃ©veloppeurs vont travailler sur leurs repos respectifs, Ã  savoir `marketplace-frontend` et `marketplace-backend`. En parallÃ¨le, il va falloir crÃ©er un repo appelÃ© `marketplace-infrastructure` permettant de dÃ©finir l'infrastructure permettant d'hÃ©berger l'application (base de donnÃ©es, load balancer, etc..). Enfin, un repo `infrastructure-shared-modules` sera crÃ©Ã© afin de gÃ©rer et versionner nos modules. Je ne vais pas m'attarder sur les questions de CI/CD et autres, ce n'est pas le sujet ici.

{{< alert  >}}
J'ai volontairement simplifiÃ© l'architecture pour l'exemple, en omettant toute la configuration IAM, Landing Zone et autres. Ici, je me focus uniquement sur la configuration Terragrunt.
{{< /alert >}}

### SchÃ©ma d'architecture

SchÃ©matiquement, on se retrouve avec quelque chose de classique :

![scheme](./imgs/scheme.png "SchÃ©ma de l'architecture de l'application")

D'un premier abord, dans le cas oÃ¹ mon client fictif ne souhaite utiliser que deux environnements de dÃ©veloppement, pour ce projet unique, Terragrunt serais overkill. Imaginons maintenant que ce mÃªme client souhaite 4 environnements, Ã  savoir `dev`, `staging`, `preprod` et `prod`. De plus, il viens de recevoir une demande du mÃ©tier, nÃ©cessitant la mise en place de 6 applications similaires. Tu vois ou je veux en venir ?

### HiÃ©rarchie des dossiers

{{< alert "circle-info" >}}
La logique d'organisation des dossiers est directement inspirÃ©e de l'excellent [repo maintenu par Padok](https://github.com/padok-team/docs-terraform-guidelines/tree/main), fournissant des bonnes pratiques sur l'utilisation de Terraform, et notamment le concept de `layers`. Je t'invites Ã  le consulter.
{{< /alert >}}

Je sais donc que je dois livrer la marketplace en premier lieu, sur 4 environnements, mais avec en tÃªte le fait que quelques semaines plus tard la demande Ã©voluera. LÃ , on rentre dans un cas ou Terragrunt pourra m'Ãªtre utile, car je vais pouvoir **factoriser** dÃ¨s le dÃ©but. CommenÃ§ons d'abord par l'arborescence type des dossiers dans mon repo d'infrastructure :

```plaintext
â””â”€â”€ layers
    â”œâ”€â”€ certificates
    â”‚Â Â  â”œâ”€â”€ dev
    â”‚Â Â  â”œâ”€â”€ staging
    â”‚Â Â  â”œâ”€â”€ preprod
    â”‚Â Â  â”œâ”€â”€ prod
    â”œâ”€â”€ cloud-armor
    â”‚Â Â  â”œâ”€â”€ web-backend
    â”‚Â Â  â”‚Â Â  â”œâ”€â”€ dev
    â”‚Â Â  â”‚Â Â  â”œâ”€â”€ staging
    â”‚Â Â  â”‚Â Â  â”œâ”€â”€ preprod
    â”‚Â Â  â”‚Â Â  â”œâ”€â”€ prod
    â”œâ”€â”€ cloud-run
    â”‚   â”œâ”€â”€ marketplace-frontend  
    |   |   â”œâ”€â”€ dev  
    |   |   â”œâ”€â”€ staging  
    |   |   â”œâ”€â”€ preprod  
    |   |   â””â”€â”€ prod  
    |   â”œâ”€â”€ marketplace-backend  
    |   Â     â”œâ”€â”€ dev  
    |   Â     â”œâ”€â”€ staging  
    |   Â     â”œâ”€â”€ preprod  
    |   Â     â””â”€â”€ prod  
    â”œâ”€â”€ sql-database
    â”‚   â”œâ”€â”€ dev
    â”‚   â”œâ”€â”€ staging
    â”‚   â”œâ”€â”€ preprod
    â”‚   â””â”€â”€ prod
    â”œâ”€â”€ secrets
    â”‚   â”œâ”€â”€ dev
    â”‚   â”œâ”€â”€ staging
    â”‚   â”œâ”€â”€ preprod
    â”‚   â””â”€â”€ prod
    â””â”€â”€ load-balancer
        â”œâ”€â”€ dev
        â”œâ”€â”€ staging
        â”œâ”€â”€ preprod
        â””â”€â”€ prod
```

{{< alert "circle-info" >}}
Il manque quelques briques comme la gestion DNS, VPC et autres, mais je ne vais pas m'attarder lÃ -dessus. Je vais me concentrer sur la partie Terragrunt.
{{< /alert >}}

Au sein du dossier `layers`  on se retrouve donc avec un dossier par type d'asset. Au sein de chaque dossier, on va retrouver un dossier par environnement. Jusqu'ici, c'est classique, on pourrais d'ailleurs utiliser cette logique avec du Terraform vanilla. Attardons nous maintenant sur le contenu de ces dossiers.

### Fichiers de configurations Terragrunt

Au sein du dossier `layers`, Ã  la racine, je vais crÃ©er deux fichiers : `common.hcl`, ainsi que `root.hcl`.

#### `common.hcl`

Ce fichier aura pour but de dÃ©finir les `locals`, c'est Ã  dire les variables communes Ã  l'ensemble de mes environnements. On y retrouvera donc l'`id` du projet, la region, et autres. Sans plus de suspense, le voici :

```hcl
# common.hcl
locals {
  root_dir    = get_parent_terragrunt_dir()
  layers_path = "${local.root_dir}/../layers"
  environment = basename(get_original_terragrunt_dir())
  region      = "europe-west3"
  config      = lookup(local.config_by_environment, local.environment, {})
  config_by_environment = {
    dev = {                            
      project_id = "marketplace-dev"
      network    = "marketplace"
    },
    staging = {                            
      project_id = "marketplace-staging" 
      network    = "marketplace"              
    },
    preprod = {
      project_id = "marketplace-preprod"
      network    = "marketplace"
    },
    prod = {                            
      project_id = "marketplace-prod" 
      network    = "marketplace"              
    }
  }
}

inputs = {}
```

{{< alert "circle-info" >}}
Dans le cas ou l'on souhaite utiliser un Cloud Provider different, comme `AWS` par exemple, il faudra adapter la configuration des `locals` en fonction de la logique de nommage de ton provider. Par exemple, pour AWS, il faudra adapter le `region` et le `project_id` en fonction de la logique de nommage AWS (`account_id` par exemple au lieu du `project_id`).
{{< /alert >}}

Le fichier `common.hcl` va donc nous permettre de centraliser la configuration de l'ensemble de nos environnements. On y retrouve donc la configuration de chaque environnement, ainsi que le `project_id` et le `region`. On y retrouve aussi une variable `inputs`, qui va nous permettre de dÃ©finir des variables d'entrÃ©es communes Ã  l'ensemble des modules.

{{< alert "circle-info" >}}
Point important : cet example de configuration pars du principe qu'un VPC dÃ©diÃ© est crÃ©Ã© en amont pour hÃ©berger l'ensemble des ressources.
{{< /alert >}}

DÃ©taillons un peu ce fichier :

- `root_dir` : cette ligne utilise la fonction `get_parent_terragrunt_dir()` pour obtenir le chemin du rÃ©pertoire parent du fichier Terragrunt actuel. Cela permet de dÃ©finir dynamiquement le chemin racine du projet, utile pour structurer les fichiers et modules

- `layers_path`: cette ligne construit le chemin vers le dossier `layers` en utilisant la variable `root_dir`. Cela permet de rÃ©fÃ©rencer les couches ou modules Terraform situÃ©s dans un dossier spÃ©cifique (layers) adjacent au rÃ©pertoire racine.

- `environnement`: cette ligne utilise la fonction `basename()` pour extraire le nom du rÃ©pertoire oÃ¹ se trouve le fichier Terragrunt actuel. Dans notre cas, cela permet de determiner dynamiquement l'environnement (par exemple, dev, staging, etc.) en fonction du nom du dossier contenant ce fichier.

- `config`: un peu plus complexe. Cette ligne utilise la fonction `lookup()` pour rechercher une configuration spÃ©cifique Ã  l'environnement actuel (local.environment) dans la variable `config_by_environment` expliquÃ©e ci-dessous. Si une configuration pour l'environnement actuel existe, elle sera rÃ©cupÃ©rÃ©e ; sinon, une valeur par dÃ©faut (ici `{}`) sera utilisÃ©e. C'est une maniÃ¨re assez habile d'adapter automatiquement les configurations en fonction de l'environnement actuel sans avoir Ã  `hardcoder` des conditions.

- `config_by_environment`: ce bloc dÃ©finit un objet contenant les configurations spÃ©cifiques Ã  chaque environnement (par exemple, dev, staging, etc.). Ici, je l'utilise pour spÃ©cifier de maniÃ¨re globale le `project_id` et le `network` pour chaque environnement. On pourrais y ajouter d'autres configurations que l'on souhaite centraliser tel que des `tags` etc.

{{< alert "circle-info" >}}
Les fonctions Terragrunt sont trÃ¨s puissantes. Elles permettent de rendre le code `HCL` idempotent. 
{{< /alert >}}

Pas de panique, je dÃ©taillerais plus clairement la maniÃ¨re dont cela fonctionne dans les parties suivantes. Pour l'instant, retiens juste que ce fichier `common.hcl` va nous permettre de centraliser les variables communes Ã  l'ensemble de nos environnements.

#### `root.hcl`

De la mÃªme maniÃ¨re que le fichier `common.hcl`, le fichier `root.hcl` va nous permettre de centraliser certaines configurations. Garde en tÃªte que le nommage de ces fichiers est purement arbitraire, mais reflÃ¨te selon moi bien leur usage.

```hcl
# root.hcl
locals {
  # On rÃ©cupÃ¨re la configuration commune dÃ©finie dans common.hcl
  common = read_terragrunt_config(find_in_parent_folders("common.hcl")).locals
  config = local.common.config
}

remote_state {
  backend = "gcs"
  config = {
    bucket   = "${local.config.project_id}-tfstates"
    project  = local.config.project_id
    prefix   = "tfstate/${path_relative_to_include()}"
    location = local.common.region
  }
  generate = {
    path      = "backend.tf"
    if_exists = "skip"
  }
}
```

Ici, et c'est une des fonctionnalitÃ©s que j'apprÃ©cie particuliÃ¨rement, on va pouvoir dÃ©finir la configuration du `backend` de maniÃ¨re centralisÃ©e. En effet, Terragrunt va se charger de gÃ©nÃ©rer le fichier `backend.tf` pour nous dans chaque `layers`.

{{< alert "circle-info" >}}
On va donc se retrouver avec un fichier `state` par environnement, et par module. Par exemple, pour le module `cloud-run`, on va se retrouver avec un fichier `tfstate` dans le bucket `marketplace-dev-tfstates` (pour l'environnement dev), avec le prefix `tfstate/cloud-run/marketplace-frontend`.
{{< /alert >}}

### Configuration d'un `layer`

Pour le moment donc, j'ai couvert la configuration `racine`, permettant de factoriser Ã  la base de l'arborescence. Comme dit plus haut, c'est de cette maniÃ¨re que Terragrunt permet de construire une infrastructure en mode `DRY`. Descendons maintenant d'un cran dans l'arborescence, et voyons comment configurer un asset `cloud-run` par exemple.

#### Gestion des modules Terraform de faÃ§on DRY

Naviguons dans le dossier `layers/cloud-run/`. Son contenu sera le suivant :

```plaintext
â””â”€â”€ cloud-run
    â”œâ”€â”€ dev
    â”‚   â”œâ”€â”€ inputs.hcl
    â”‚   â””â”€â”€ terragrunt.hcl
    â”œâ”€â”€ module.hcl
    â”œâ”€â”€ preprod
    â”‚   â”œâ”€â”€ inputs.hcl
    â”‚   â””â”€â”€ terragrunt.hcl
    â”œâ”€â”€ prod
    â”‚   â”œâ”€â”€ inputs.hcl
    â”‚   â””â”€â”€ terragrunt.hcl
    â””â”€â”€ staging
        â”œâ”€â”€ inputs.hcl
        â””â”€â”€ terragrunt.hcl
```

CommenÃ§ons par le contenu du fichier `module.hcl`. Rappelles toi, j'ai indiquÃ© en intro qu'il Ã©tait nÃ©cessaire de crÃ©er un repo `infrastructure-shared-modules` pour y stocker mes modules. C'est ici que je vais les rÃ©fÃ©rencer :

```hcl
# module.hcl
terraform {
  source = "git@github.com:my-org/infrastructure-shared-modules.git//modules/cloudrun?ref=cloudrun-v1.0.0"
}
```

ConcrÃ¨tement, mon repo `infrastructure-shared-modules` va contenir l'ensemble de mes modules, versionnÃ©s via `git`.

{{< alert >}}
Je te conseilles vivement de `tag` tes modules, afin de pouvoir revenir en arriÃ¨re si besoin. En effet, si tu fais Ã©voluer un module, et que tu ne tag pas ta version, tu risques d'appliquer les changements Ã  l'ensemble des configurations.
{{< /alert >}}

En backstage, Terragrunt va cloner ce `repo`, et lui fournir les `inputs` dÃ©finies dans chaque fichiers `inputs.hcl`. On retrouvera d'ailleurs un dossier `.terragrunt-cache`, contenant le repo clonÃ© dans chaque environnement.

#### Que se passe-t-il lorsque je lance un `terragrunt apply` ?

Un petit coup d'oeil sur le fichier `terragrunt.hcl` permet de se rendre compte de toute la logique expliquÃ©e prÃ©cÃ©demment :

```hcl
include "root" {
  path           = find_in_parent_folders("root.hcl")
  merge_strategy = "deep"
}

include "common" {
  path           = find_in_parent_folders("common.hcl")
  merge_strategy = "deep"
}

include "module" {
  path           = find_in_parent_folders("module.hcl")
  merge_strategy = "deep"
}

include "inputs" {
  path           = "inputs.hcl"
  merge_strategy = "deep"
}
```

Ce qui se rÃ©sume schÃ©matiquement :

{{< mermaid >}}
flowchart TD
    A[Configuration finale] --> B[root.hcl]
    A --> C[common.hcl]
    A --> D[module.hcl]
    A --> E[inputs.hcl]
{{< /mermaid >}}

Terragrunt parcourra depuis le dossier courant, et remontera dans l'arborescence jusqu'Ã  trouver les fichiers `root.hcl`, `common.hcl`, `module.hcl` et `inputs.hcl`. Il va ensuite fusionner le contenu de ces fichiers, en appliquant la stratÃ©gie de fusion dÃ©finie dans chaque `include`. Dans notre cas, on a choisi la stratÃ©gie `deep`, qui va permettre de fusionner les objets imbriquÃ©s. La [documentation de Terragrunt](https://terragrunt.gruntwork.io/docs/reference/config-blocks-and-attributes/#include) permettra d'en savoir plus sur les diffÃ©rentes stratÃ©gies de fusion, et le fonctionnement d'include en gÃ©nÃ©ral.

{{< alert "circle-info" >}}
Pour faire simple, on peut partir du principe que Terragrunt va fusionner les fichiers de configuration en un seul fichier, en appliquant la stratÃ©gie de fusion dÃ©finie dans chaque `include`. Il va ensuite appliquer cette configuration au module Terraform. Le fichier `input.hcl` contiendra donc l'Ã©quivalent d'un fichier `variables.tf` dans un module Terraform classique. Il va permettre de dÃ©finir les variables d'entrÃ©es pour le module, et sera fusionnÃ© avec les autres fichiers de configuration.
{{< /alert >}}

#### Le concept de `terragrunt run-all

Cette commande va permettre d'appliquer l'ensemble des configurations de maniÃ¨re rÃ©cursive. Par exemple, si je me trouve dans le dossier `layers/, et que je lance la commande suivante :

```bash
terragrunt run-all apply .
```



## Pour conclure

Je t'avais prÃ©venu, la courbe d'apprentissage est assez raide. Mais concrÃ¨tement, la difficultÃ© se situe vraiment dans la comprÃ©hension de la logique de `merge` de Terragrunt. Pour maintenir une infrastructure `DRY`, Terragrunt utilise des fonctions et des `includes` pour fusionner les fichiers de configuration Ã  l'`apply`.

Tu l'as bien compris, la mise en place d'une architecture `DRY` avec Terragrunt reste relativement complexe de part l'abstraction qu'elle propose. Selon moi, la valeur ajoutÃ©e vaut le coup si :

- Ton projet possÃ¨de de nombreux environnements Ã  gÃ©rer (dev, staging, prod, etc..).
- Tu dois gÃ©rer un grand nombre d'applications/infrastructures identiques, mais dont la configuration diffÃ¨re par environnement.
- Tu souhaites gÃ©rer des modules Terraform de maniÃ¨re centralisÃ©e, et versionnÃ©e, et les appliquer Ã  l'ensemble de tes environnements.

